prototype(Networkteam.Neos.SelectiveMaintenance:MaintenanceContent) < prototype(Neos.Fusion:Case) {
    @context {
        node = ${q(documentNode).property('inheritMaintained') ? q(documentNode).parents('[instanceof Networkteam.Neos.SelectiveMaintenance:Mixin.Maintenance]').filter('[inheritMaintained != true]').get(0) || site : documentNode}
    }

    live {
        @context {
            maintained = ${q(node).property('maintained') && q(node).property('maintenanceDocument')}
            // for use in Neos.Neos:ContentCollection
            node = ${q(node).property('maintenanceDocument')}
            nodePath = ${String.isBlank(q(node).property('maintenanceContentNodePath')) ? 'main' : q(node).property('maintenanceContentNodePath')}
        }
        condition = ${!node.context.inBackend && maintained}
        renderer = Neos.Neos:ContentCollection {
            nodePath = ${nodePath}
        }
    }

    inBackend {
        condition = ${node.context.inBackend && q(node).property('maintained') && q(node).property('maintenanceDocument')}
        renderer = Neos.Fusion:Join {
            message = Networkteam.Neos.SelectiveMaintenance:MaintenanceMessage
            content = ${value}
        }
    }

    default {
        condition = true
        renderer = ${value}
    }
}